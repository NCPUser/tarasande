plugins {
    id "java"
    id "fabric-loom"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow"
}

group 'net.tarasandedevelopment'
version tarasande_version

repositories {
    mavenCentral()
}

loom {
    accessWidenerPath = file("src/main/resources/tarasande-protocol-spoofer.accesswidener")
}

this.remapJar {
    dependsOn shadowJar
    //noinspection GrDeprecatedAPIUsage
    input.set(shadowJar.archiveFile)
}

this.shadowJar {
    archiveClassifier.set("shadow")
    compileJava.options.encoding = "UTF-8"
    exclude "native-binaries/*"

    dependencies {
        include(dependency("io.netty:netty-codec-haproxy:4.1.82.Final"))
    }
}

// ViaProtocolHack READ-ME
var viaLibs = [
        "com.viaversion:viaversion:${project.viaversion_version}",

        "com.github.RejectedVia:ViaProtocolHack:${project.viaprotocolhack_version}"
]

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Kotlin
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.kotlin_wrapper_version}"

    // tarasande Source
    implementation(project(path: ":", configuration: "shadow"))
    implementation(project(path: ":package-protocol-hack", configuration: "shadow"))

    // HAProxy Protocol -> https://mvnrepository.com/artifact/io.netty/netty-codec-haproxy
    implementation("io.netty:netty-codec-haproxy:4.1.82.Final")

    for (final def via in viaLibs) {
        implementation(via)
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"
