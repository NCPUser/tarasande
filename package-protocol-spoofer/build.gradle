plugins {
    id "java"
    id "fabric-loom"
    id "org.jetbrains.kotlin.jvm"
}

group 'net.tarasandedevelopment'
version tarasande_version

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
}

loom {
    accessWidenerPath = file("src/main/resources/tarasande-protocol-spoofer.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation project(":")
    implementation ("com.github.FlorianMichael:ViaFabricPlus:${project.viafabricplus_version}") {
        exclude group: "com.terraformersmc"
    }

    include "io.netty:netty-codec-haproxy:4.1.82.Final"
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"
