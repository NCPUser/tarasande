plugins {
    id "java"
    id "fabric-loom"
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow"
}

group 'net.tarasandedevelopment'
version tarasande_version

repositories {
    mavenCentral()
    maven {
        name = "opencollab-releases"
        url = "https://repo.opencollab.dev/maven-releases/"
    }
    maven {
        name = "opencollab-snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
}

loom {
    accessWidenerPath = file("src/main/resources/tarasande-protocol-hack.accesswidener")
}

var libArray = [
        "com.viaversion:viaversion:${project.viaversion_version}",
        "com.viaversion:viabackwards-common:${project.viabackwards_version}",
        "org.yaml:snakeyaml:1.29",

        "com.github.FlorianMichael:ViaLoadingBase:${project.vialoadingbase_version}",

        "net.lenni0451.mcstructs:text:2.1.0",
        "org.jsoup:jsoup:1.15.3",

        // Nukkit -> CloudBurst, Co
        "com.nukkitx.protocol:common:2.9.15-SNAPSHOT",
        "com.nukkitx.protocol:bedrock-v560:2.9.15-SNAPSHOT",
        "com.nimbusds:nimbus-jose-jwt:9.13",
        "org.lanternpowered:lmbda:2.0.0"
]

this.remapJar {
    dependsOn shadowJar
    //noinspection GrDeprecatedAPIUsage
    input.set(shadowJar.archiveFile)
}

this.shadowJar {
    archiveClassifier.set("shadow")
    compileJava.options.encoding = "UTF-8"
    exclude "native-binaries/*"

    dependencies {
        for (final def lib in libArray) {
            include(dependency(lib))
        }
        include(dependency("com.nukkitx.protocol:.*"))
        include(dependency("com.nukkitx.network:.*"))
        include(dependency("com.nukkitx.fastutil:.*"))
    }
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    for (final def via in libArray) {
        implementation(via)
    }

    // tarasande Source
    implementation(project(path: ":", configuration: "shadow"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"
